plugins {
   id 'java'
   id 'org.jbake.site' version "5.5.0"
   id "org.xbib.gradle.plugin.git" version "2.0.0"
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0' 
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {  
    ignoreFailures = true 
    useJUnitPlatform()
}

task reports (dependsOn: ['check', 'javadoc']) {
   description 'Generate all reports for this project'
}


task copyWebPages (type: Copy) {     
    from 'src/main/html'
    into 'build/reports'
}

task copyDocs (type: Copy) {            
    from 'build/docs'
    into 'build/reports/docs'
    dependsOn 'javadoc'
}

task buildSite (dependsOn: ['reports','copyWebPages', 'copyDocs']) {
    description 'Generate reports website for this project'
}

task clonePages() {                                            
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString()
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = 'file:' + thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
        grgit.checkout {
            branch = 'gh-pages'
        }
    }
}

task copyReports (type: Copy, dependsOn: ['buildSite', 'clonePages']) {  
    from "$buildDir/reports"
    into "$buildDir/gh-pages"
}

task updateGHPages (dependsOn: 'copyReports') {                     
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir 
        }
        grgit.add (update: false, patterns: ['reports/'])
        grgit.add (update: true, patterns: ['reports/'])
        grgit.commit {
            message = "Updating web pages"
        }
        grgit.push {}

    }
}